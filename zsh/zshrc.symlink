# Print debug info when loading shell
DEBUG=true

# shortcut to this dotfiles path is $ZSHDOT
export ZSHDOT=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Sites

# shortcut to oh-my-zsh path in $ZSH
export ZSH=$HOME/.oh-my-zsh

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# Download the latest configuration in the background. 
# This will just dump either an "ok" or "not ok" message 
# into the termal when done
$DEBUG && echo Updating dotfiles
cd $ZSHDOT && git pull --ff-only &

# all of our zsh files
typeset -U config_files
config_files=($ZSHDOT/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  $DEBUG && echo "Loading path $file"
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  $DEBUG && echo "Loading $file"
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  $DEBUG && echo "Loading completion $file"
  source $file
done

unset config_files

# Open new tabs in same working directory
precmd () {print -Pn "\e]2; %~/ \a"}
preexec () {print -Pn "\e]2; %~/ \a"}

### Added by the Heroku Toolbelt
export PATH="$PATH:/usr/local/heroku/bin"

# added by travis gem
[ -f "$HOME/.travis/travis.sh" ] && source "$HOME/.travis/travis.sh"
